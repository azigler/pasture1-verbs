@create $root_class named MCP 2.1:MCP 2.1,mcp
@verb #110:"create_session" this none this
@program #110:create_session
{connection} = args;
if (caller != this)
  raise(E_PERM);
elseif (typeof(session = this.session:new()) != ANON)
  raise(session);
endif
session:set_connection(connection);
session:initialize_connection();
return session;
.

@verb #110:"destroy_session" this none this
@program #110:destroy_session
{session} = args;
if (!(caller in {this, session}))
  raise(E_PERM);
elseif (!$object_utils:isa(session, this.session))
  raise(E_INVARG);
elseif (session == this.session)
  raise(E_INVARG);
else
  $recycler:_recycle(session);
endif
.

@verb #110:"initialize_connection" this none this rxd #2
@program #110:initialize_connection
{who} = args;
if (caller != this)
  raise(E_PERM);
endif
return this:create_session(who);
.

@verb #110:"finalize_connection" this none this rxd #2
@program #110:finalize_connection
{con} = args;
if (caller == con)
  this:destroy_session(con);
endif
.

@verb #110:"parse_version" this none this rxd #2
@program #110:parse_version
"string version number -> {major, minor}";
{version} = args;
if (m = match(version, "%([0-9]+%)%.%([0-9]+%)"))
  return {toint(substitute("%1", m)), toint(substitute("%2", m))};
endif
.

@verb #110:"compare_version_range" this none this rxd #2
@program #110:compare_version_range
{client, server} = args;
{min1, max1} = client;
{min2, max2} = server;
min1 = (typeof(min1) == STR) ? this:parse_version(min1) | min1;
min2 = (typeof(min2) == STR) ? this:parse_version(min2) | min2;
max1 = (typeof(max1) == STR) ? this:parse_version(max1) | max1;
max2 = (typeof(max2) == STR) ? this:parse_version(max2) | max2;
if (!(((min1 && min2) && max1) && max2))
  return;
else
  if ((this:compare_version(max1, min2) <= 0) && (this:compare_version(max2, min1) <= 0))
    if (this:compare_version(max1, max2) < 0)
      return max2;
    else
      return max1;
    endif
  endif
endif
return 0;
.

@verb #110:"compare_version" this none this rxd #2
@program #110:compare_version
"-1 if v1 > v2, 0 if v1 = v2, 1 if v1 < v2";
{v1, v2} = args;
if (v1 == v2)
  return 0;
else
  {major1, minor1} = v1;
  {major2, minor2} = v2;
  if (major1 == major2)
    if (minor1 > minor2)
      return -1;
    else
      return 1;
    endif
  elseif (major1 > major2)
    return -1;
  else
    return 1;
  endif
endif
.

@verb #110:"unparse_version" this none this rxd #2
@program #110:unparse_version
{major, minor} = args;
return tostr(major, ".", minor);
.

@verb #110:"session_for" this none this rxd #2
@program #110:session_for
{who} = args;
return `who.out_of_band_session ! E_PROPNF => $failed_match';
.

@verb #110:"user_created user_connected user_reconnected" this none this rxd #2
@program #110:user_created
{who} = args;
if (listeners(caller))
  if ($recycler:valid(who.out_of_band_session))
    `who.out_of_band_session:finish() ! ANY';
  endif
  who.out_of_band_session = this:initialize_connection(who);
endif
.

@verb #110:"user_disconnected user_client_disconnected" this none this rxd #2
@program #110:user_disconnected
{who} = args;
if (listeners(caller))
  if ($recycler:valid(who.out_of_band_session))
    `who.out_of_band_session:finish() ! ANY';
    who.out_of_band_session = $nothing;
  endif
else
  raise(E_PERM);
endif
.

@verb #110:"do_out_of_band_command" this none this rxd #2
@program #110:do_out_of_band_command
if (listeners(caller))
  if ($recycler:valid(session = player.out_of_band_session))
    set_task_perms(player);
    return session:do_out_of_band_command(@args);
  endif
endif
.

@verb #110:"package_name match_package packges" this none this
@program #110:package_name
return this.registry:(verb)(@args);
.

@verb #110:"handles_package wait_for_package" this none this
@program #110:handles_package
{who, @rest} = args;
if (valid(session = this:session_for(who)))
  return session:(verb)(@rest);
endif
.

@verb #110:"include_for_core" this none this rxd #2
@program #110:include_for_core
return {"parser", "session", "package", "negotiate", "registry", "cord", "simpleedit", "client", "status", "dispatch"};
.

@prop #110."parser" #107 rc
@prop #110."session" #108 r
@prop #110."version" {} rc
;;#110.("version") = {2, 1}
@prop #110."package" #104 r #2
@prop #110."negotiate" #105 r #2
@prop #110."registry" #109 rc
@prop #110."cord" #106 r
@prop #110."simpleedit" #113 rc
@prop #110."client" #121 rc
@prop #110."status" #123 rc
@prop #110."dispatch" #103 rc
;;#110.("aliases") = {"MCP 2.1", "mcp"}
;;#110.("object_size") = {11136, 1670400000}
