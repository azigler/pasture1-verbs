@create $feature named Stage-Talk Feature:Stage-Talk Feature
@verb #89:"`* -* to*" any any any rxd
@program #89:`
"Say something out loud, directed at someone or something.";
"Usage:";
"  `target message";
"Example:";
"  Munchkin is talking to Kenneth, who's in the same room with him.  He types:";
"      `kenneth What is the frequency?";
"  The room sees:";
"       Munchkin [to Kenneth]: What is the frequency?";
name = verb[2..$];
who = player.location:match_object(name);
if ($command_utils:object_match_failed(who, name))
  return;
endif
player.location:announce_all(player.name, " [to ", who.name, "]: ", argstr);
.

@verb #89:"stage [*" any any any rxd
@program #89:stage
"Say something out loud, in some specific way.";
"Usage:";
"  [how]: message";
"Example:";
"  Munchkin decideds to sing some lyrics.  He types:";
"      [sings]: I am the eggman";
"  The room sees:";
"      Munchkin [sings]: I am the eggman";
player.location:announce_all((((player.name + " ") + verb) + " ") + argstr);
.

@verb #89:"stage ]*" any any any rxd
@program #89:stage
"Perform some physical, non-verbal, action.";
"Usage:";
"  ]third person action";
"Example:";
"  Munchkin has annoyed some would-be tough guy.  He types:";
"      ]hides behind the reactor.";
"  The room sees:";
"      [Munchkin hides behind the reactor.]";
player.location:announce_all("[", (((player.name + " ") + verb[2..$]) + (argstr ? " " + argstr | "")) + "]");
.

@verb #89:"~*" any any any rxd
@program #89:~
name = verb[2..$];
argstr = $code_utils:argstr(verb, args, argstr);
player.location:announce_all(player.name, " [", name, "]: ", argstr);
.

@verb #89:"stage <*" any any any rxd
@program #89:stage
"Point to yourself.";
"Usage:";
"  <message";
"Example:";
"  Muchkin decides he's being strange. He types:";
"    <being strange.";
"  The room sees:";
"    Munchkin <- being strange.";
player.location:announce_all((((player.name + " <- ") + verb[2..$]) + " ") + argstr);
.

;;#89.("help_msg") = {"This feature contains various verbs used in stage talk, which allows players to describe their actions in terms of stage directions instead of prose."}
;;#89.("feature_verbs") = {"`", "[", "]", "-", "<"}
;;#89.("aliases") = {"Stage-Talk Feature"}
;;#89.("description") = {"This feature contains various verbs used in stage talk, which allows players to describe their actions in terms of stage directions instead of prose."}
;;#89.("object_size") = {6159, 1641541193}
